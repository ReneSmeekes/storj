// dbx.v1 golang satellitedb.dbx .

model bwagreement (
	key signature 

	field signature blob 

	field data blob
	
	field created_at timestamp ( autoinsert )
)

create bwagreement ( )
delete bwagreement ( where bwagreement.signature = ? )
read one (
  select bwagreement 
  where  bwagreement.signature = ?
)
read limitoffset (
	select bwagreement
)
read all (
	select bwagreement
)
read all (
	select bwagreement
	where  bwagreement.created_at > ?
)

// datarepair.irreparableDB
model irreparabledb (
	key segmentpath

	field segmentpath blob
	field segmentdetail blob (updatable)
	field pieces_lost_count int64 (updatable)
	field seg_damaged_unix_sec int64 (updatable)
	field repair_attempt_count int64 (updatable)
)

create irreparabledb ( )
update irreparabledb ( where irreparabledb.segmentpath = ? )
delete irreparabledb ( where irreparabledb.segmentpath = ? )
read one (
  select irreparabledb 
  where  irreparabledb.segmentpath = ?
)

// dbx.v1 golang statdb.dbx .
model node (
	key id

	field id blob
	field audit_success_count int64 (updatable)
	field total_audit_count int64 (updatable)
	field audit_success_ratio float64 (updatable)

	field uptime_success_count int64 (updatable)
	field total_uptime_count int64 (updatable)
	field uptime_ratio float64 (updatable)

	field created_at timestamp ( autoinsert )
	field updated_at timestamp ( autoinsert, autoupdate )
)

create node ( )
update node ( where node.id = ? )
delete node ( where node.id = ? )
read one (
  select node
  where  node.id = ?
)
